import com.v7878.zygisk.gradle.ZygoteLoader

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.java.zygisk)
}

def updateJsonBase = "https://raw.githubusercontent.com/vova7878-modules/PMPatch/main/updates/"

android {
    namespace 'com.v7878.hooks.pmpatch'
    compileSdk 35

    defaultConfig {
        minSdk 26
        targetSdk 35
        versionCode 1_000_000
        versionName "v1.0.0"
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
    }

    flavorDimensions += ["patch"]
    productFlavors {
        patch12 {
            dimension "patch"

            zygisk {
                packages(ZygoteLoader.ALL_PACKAGES)

                id = "com_v7878_pmpatch_12"
                updateJson = "${updateJsonBase}update_12.json"
                archiveName = "PMPatch12"
            }

            buildConfigField("boolean", "PATCH_1", "true")
            buildConfigField("boolean", "PATCH_2", "true")
            buildConfigField("boolean", "PATCH_3", "false")
        }
        patch3 {
            dimension "patch"

            zygisk {
                packages(ZygoteLoader.PACKAGE_SYSTEM_SERVER)

                id = "com_v7878_pmpatch_3"
                updateJson = "${updateJsonBase}update_3.json"
                archiveName = "PMPatch3"
            }

            buildConfigField("boolean", "PATCH_1", "false")
            buildConfigField("boolean", "PATCH_2", "false")
            buildConfigField("boolean", "PATCH_3", "true")
        }
        full {
            dimension "patch"

            zygisk {
                packages(ZygoteLoader.ALL_PACKAGES)

                id = "com_v7878_pmpatch_full"
                updateJson = "${updateJsonBase}update_full.json"
                archiveName = "PMPatchFull"
            }

            buildConfigField("boolean", "PATCH_1", "true")
            buildConfigField("boolean", "PATCH_2", "true")
            buildConfigField("boolean", "PATCH_3", "true")
        }
        legacy {
            dimension "patch"

            zygisk {
                packages(ZygoteLoader.PACKAGE_SYSTEM_SERVER)

                id = "com_v7878_pmpatch"
                updateJson = "${updateJsonBase}update.json"
                archiveName = "PMPatchLegacy"
            }

            buildConfigField("boolean", "PATCH_1", "true")
            buildConfigField("boolean", "PATCH_2", "true")
            buildConfigField("boolean", "PATCH_3", "true")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

zygisk {
    name = "PMPatch"
    author = "v7878"
    description = "Disable signature verification For Android"
    entrypoint = "com.v7878.hooks.pmpatch.Main"
}

dependencies {
    implementation libs.androidvmtools
    implementation libs.r8.annotations
}